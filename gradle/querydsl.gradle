// 1. Setup dependencies
configurations {
    querydslapt
}

dependencies {
    // Spring Data 1.2.0.RELEASE contained the update to QueryDSL 2.8 - http://docs.spring.io/spring-data/jpa/docs/1.3.x/changelog.txt
    def querydslVersion = '2.8.0'

    // for the anontation processor
    querydslapt group: 'com.mysema.querydsl', name: 'querydsl-jpa', version: querydslVersion, classifier: 'apt-one-jar', transitive: false

    // for compiling
    compile (group: 'com.mysema.querydsl', name: 'querydsl-jpa', version: querydslVersion) {
		exclude(module: 'asm') // asm-4.x included by kryo library
    }
    provided group: 'com.mysema.querydsl', name: 'querydsl-jpa', version: querydslVersion, classifier: 'apt'
    provided group: 'com.mysema.querydsl', name: 'querydsl-apt', version: querydslVersion
}

// 2. Create the generated classes directory
def generatedSrcDir = 'src/main/generated'
task createGeneratedSrcDir << {
    file(generatedSrcDir).mkdirs()
}
compileJava.dependsOn createGeneratedSrcDir

idea.module.sourceDirs += file(generatedSrcDir)

task copyFactoryPath << {
    copy {
        from('src/build/eclipse') {
            include ".factorypath"
        }
        into '.'
        expand(querydslapt: configurations.querydslapt.asPath)
    }
    copy {
        from('src/build/eclipse') {
            include ".settings/org.eclipse.jdt.apt.core.prefs"
        }
        into '.'
        expand(generatedSrcDir: generatedSrcDir)
    }
}
eclipseClasspath.dependsOn copyFactoryPath

// 3. Add annotation proccesing to the compileJava Task
compileJava.options.compilerArgs = ["-s", file(generatedSrcDir).absolutePath]
clean {
    delete generatedSrcDir
}

// 4. turn on annotaion processsing in eclipse
task configureEclipseAPT << {
    // Write the .factorypath file
    project.file('.factorypath').withWriter{ writer ->
        def xml = new groovy.xml.MarkupBuilder(writer)
        xml.setDoubleQuotes(true)
        xml.factorypath() {
            factorypathentry(kind: "EXTJAR", id: configurations.querydslapt.asPath, enabled: "true", runInBatchMode: "false")
        }
    }

    // Write the org.eclipse.jdt.apt.core.prefs file:
    project.file('.settings').mkdirs()
    project.file('.settings/org.eclipse.jdt.apt.core.prefs').withWriter{ writer ->
        def props = new Properties()
        props.put('eclipse.preferences.version', '1')
        props.put('org.eclipse.jdt.apt.aptEnabled', 'true')
        props.put('org.eclipse.jdt.apt.genSrcDir', generatedSrcDir)
        props.put('org.eclipse.jdt.apt.reconcileEnabled', 'true')
        props.store(writer, null)
    }
}
eclipseClasspath.dependsOn configureEclipseAPT

// enable process annotations
eclipse.jdt.file {
    withProperties { props ->
        props.put('org.eclipse.jdt.core.compiler.processAnnotations', 'enabled')
    }
}
